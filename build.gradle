buildscript {
    ext {
        springBootVersion = '2.2.8.RELEASE'
        querydslPluginVersion = "1.0.10"
    }
    repositories {
        mavenCentral()
        maven { url "https://plugins.gradle.org/m2/" } // plugin 저장소
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "io.spring.gradle:dependency-management-plugin:1.0.7.RELEASE"
        classpath("gradle.plugin.com.ewerk.gradle.plugins:querydsl-plugin:${querydslPluginVersion}")
    }
}
plugins {
    id 'com.google.cloud.tools.jib' version '2.5.0' apply false
}
def querydslProjects = [
    project(":api-member"), project(":api-system")/*, project(":api-auth")*/
]

configure(querydslProjects) {

    apply plugin: "com.ewerk.gradle.plugins.querydsl"

    def querydslSrcDir = 'src/main/generated'

    querydsl {
        library = "com.querydsl:querydsl-apt"
        jpa = true
        querydslSourcesDir = querydslSrcDir
    }

    compileQuerydsl{
        options.annotationProcessorPath = configurations.querydsl
    }
    
    configurations {
        querydsl.extendsFrom compileClasspath
    }

    sourceSets {
        main {
            java {
                srcDirs = ['src/main/java', querydslSrcDir]
            }
        }
    }
    
    tasks.withType(JavaCompile) {
        options.annotationProcessorGeneratedSourcesDirectory = file(querydslSrcDir)
    }
}

subprojects {
    apply plugin: "java"
    apply plugin: "eclipse"
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"
    apply plugin: "com.google.cloud.tools.jib"

    group = "com.test"
    version = "0.0.2-SNAPSHOT"

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8

    repositories {
        mavenCentral()
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    ext {
        set("springCloudVersion", "Hoxton.SR4")
    }


    task initSourceFolders {
        sourceSets*.java.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }

        sourceSets*.resources.srcDirs*.each {
            if( !it.exists() ) {
                it.mkdirs()
            }
        }
    }

    dependencies {
        compileOnly "org.projectlombok:lombok"
        annotationProcessor "org.projectlombok:lombok"
        implementation "org.apache.commons:commons-lang3:3.9"
        implementation "net.logstash.logback:logstash-logback-encoder:6.4"
        
    }

    // project 별로 의존 라이브러리 구성을 설정
    if (project.name.equals("api-domain")) {
        dependencies {
            testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.2"
        }
    } else {
        dependencies {
            implementation "org.springframework.boot:spring-boot-starter-actuator"
            developmentOnly "org.springframework.boot:spring-boot-devtools"
            testImplementation("org.springframework.boot:spring-boot-starter-test") {
                exclude group: "org.junit.vintage", module: "junit-vintage-engine"
            }
        }
    }
    
    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }
}

project (":api-common") {
    dependencies {
        //implementation (project (":api-domain"))
        ///implementation "org.springframework.boot:spring-boot-starter-data-jpa"
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.owasp.encoder:encoder:1.2.2"
        //implementation "org.postgresql:postgresql"
    }
}

project (":api-domain") {
    dependencies {
    }
}
/*
project (":api-geteway") {
    version = "0.0.3"
    jib {
        from {
            image = "docker://amazoncorretto:8"
        }
        to {
            image = "kijaha/api-gateway"
            tags = ["${project.version}".toString()]
            credHelper = "pass"
        }
        container {
            mainClass = "com.test.geteway.MsaApiGetewayApplication"
            ports = ["8090"]
            volumes = ["/tmp"]
            creationTime = "USE_CURRENT_TIMESTAMP"
        }
    }
    dependencies {
        implementation "org.springframework.cloud:spring-cloud-starter-netflix-zuul"
    }
}
*/
project (":api-member") {
    dependencies {
        implementation (project (":api-domain"))
        implementation "org.springframework.boot:spring-boot-starter-data-jpa"
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.postgresql:postgresql"
        implementation "com.querydsl:querydsl-jpa"
        implementation "com.querydsl:querydsl-apt"
        implementation "com.querydsl:querydsl-core"
        implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
        implementation "org.springframework.cloud:spring-cloud-starter-zipkin"
        implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.3"
    }
}

project (":api-system") {
    dependencies {
        implementation (project (":api-common"))
        // 추후 공통으로 이동 필요
        implementation "org.modelmapper:modelmapper:2.3.7"
        implementation "org.springframework.boot:spring-boot-starter-data-jpa"
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.postgresql:postgresql"
        implementation "com.querydsl:querydsl-jpa"
        implementation "com.querydsl:querydsl-apt"
        implementation "com.querydsl:querydsl-core"
        implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-client"
        implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.3"
        implementation "org.springframework.cloud:spring-cloud-starter-oauth2"
        implementation "org.springframework.cloud:spring-cloud-starter-zipkin"
        implementation "org.springframework.cloud:spring-cloud-starter-openfeign"
    }
}
/*
project (":api-service-discovery") {
    dependencies {
        implementation "org.springframework.cloud:spring-cloud-starter-netflix-eureka-server"
        implementation "io.micrometer:micrometer-registry-prometheus"
    }
}
*/

project (":api-auth") {
    dependencies {
        implementation "org.springframework.boot:spring-boot-starter-web"
        implementation "org.springframework.boot:spring-boot-starter-data-jpa"
        implementation "com.querydsl:querydsl-jpa"
        implementation "com.querydsl:querydsl-apt"
        implementation "com.querydsl:querydsl-core"
        runtimeOnly "com.h2database:h2"
        implementation "org.springframework.cloud:spring-cloud-starter-oauth2"
        implementation "org.springframework.cloud:spring-cloud-starter-security"
        implementation "com.github.ulisesbocchio:jasypt-spring-boot-starter:3.0.3"
    }
}